apply plugin: 'java'
apply plugin: 'com.chrisgahlert.gradle-dcompose-plugin'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    testCompile \
        'junit:junit:4.10',
        'info.cukes:cucumber-java8:1.2.5',
        'info.cukes:cucumber-junit:1.2.5',
        'com.mashape.unirest:unirest-java:1.4.9',
        'org.skyscreamer:jsonassert:1.4.0',
        'net.javacrumbs.json-unit:json-unit:1.16.0',
        'net.javacrumbs.json-unit:json-unit-fluent:1.16.0',
        'org.apache.jena:apache-jena:3.2.0'
}

dcompose {
    createComposeFile.useTags = true
    registry ("$registryURL") {}

    shared {
        forcePull = true
        forceRemoveImage = true
        image = "${imageRepo}:${imageRepoPort}/uv-attx-shared"
    }
    
    mysql {
        forcePull = true
        forceRemoveImage = true
        image = 'tenforce/unified-views-mariadb:feat-compact-modular'
        env = ['MYSQL_ROOT_PASSWORD=iamroot!']
        portBindings = ['3306:3306']
    }

    backend {
        forcePull = true
        forceRemoveImage = true
        image = 'tenforce/unified-views-backend:latest'
        volumesFrom =  [shared]
        dependsOn = [mysql, shared]
    }

    uvdpus {
        forcePull = true
        forceRemoveImage = true
        image = 'tenforce/unified-views-add-dpus'
        dependsOn = [mysql]
    }

    frontend {
        forcePull = true
        forceRemoveImage = true
        image = 'tenforce/unified-views-frontend:latest'
        volumesFrom = [shared]
//        exposedPorts = ['8080']
        portBindings = ['8080:8080']
        dependsOn = [backend, shared]
    }

    wfapi {
        forcePull = true
        forceRemoveImage = true
        image = "${imageRepo}:${imageRepoPort}/wf-api:latest"
//        exposedPorts = ['4301']
        portBindings = ['4301:4301']
        dependsOn = [mysql]
    }

    test {
        ignoreExitCode = true
        baseDir = file('.')
        dockerFilename = 'Dockerfile'
        binds = ["/var/run/docker.sock:/run/docker.sock"]
        command = ['sh', '-c', '/tmp/runTests.sh']
        dependsOn = [wfapi, frontend, backend, shared, uvdpus, mysql]
        waitForCommand = true
        forceRemoveImage = true
        attachStdout = true
    }
}

task copyReportFiles(type: DcomposeCopyFileFromContainerTask) {
    service = dcompose.test
    containerPath = '/tmp/build/reports'
    destinationDir = file("build/from-container/")
    cleanDestinationDir = false
}

startTestContainer.finalizedBy copyReportFiles

// making sure the that fresh build of test classes is done before building the image
buildTestImage.dependsOn shadowJar
buildTestImage.dependsOn testClasses

shadowJar {
    classifier = 'tests'
    from sourceSets.test.output
    configurations = [project.configurations.testRuntime]
}

task integTest {
    dependsOn startTestContainer
    finalizedBy removeImages
}

task runTestsAgainstSomething(type: Test) {
    dependsOn startSharedContainer, startMysqlContainer, startBackendContainer, startFrontendContainer, startWfapiContainer, startUvdpusContainer

    doFirst {
        systemProperty 'frontend.port', dcompose.frontend.findHostPort(8080)
        systemProperty 'frontend.host', dcompose.frontend.dockerHost
        systemProperty 'wfapi.port', dcompose.wfapi.findHostPort(4301)
        systemProperty 'wfapi.host', dcompose.wfapi.dockerHost
    }
    doLast {
        systemProperties.remove 'frontend.port'
        systemProperties.remove 'frontend.host'
        systemProperties.remove 'wfapi.port'
        systemProperties.remove 'wfapi.host'
    }

    finalizedBy removeImages
}
